
import crack.ascii isSpace;
import crack.cont.array Array;
import crack.cont.hashmap HashMap;
#import crack.ext._readline readline;
import crack.io cout, cerr, Formatter, FStr, StringFormatter;
import crack.functor Functor2;
import crack.lang AppendBuffer, Exception;

class EvalContext;

## A value stored in an EvalContext.  We don't store data values directly,
## they always are evaluated.  In this way, we can do things like override
## evaluation.
@abstract class Value {

    ## Returns the value of the object.
    @abstract Object evaluate(EvalContext context);

    ## Set the value.
    @abstract void set(Object value);

    ## Invoke the function associated with the value with the given arguments.
    @abstract Object invoke(EvalContext context, Array[Object] args);
}

class EvalContext {
    EvalContext parent;
    HashMap[String, Value] vars = {};

    ## The current block.
    Array[Value] block;

    ## The instruction pointer.  This is a an index into block indicating the
    ## next instruction to evaluated.
    uint ip;

    ## Set this to true to cause evaluation to yield.
    bool yield;

    Value lookUp(String name) {
        val := vars.get(name, null);
        if (val is null) {
            if (parent)
                return parent.lookUp(name);
            else
                return null
        } else {
            return val;
        }
    }

    void define(String name, Value val) {
        vars[name] = val;
    }

    void set(String name, Object val) {
        # XXX needs to do a lookup and a set.
        holder := lookUp(name);
        if (holder is null)
            throw Exception(FStr() I`Undefined variable $name can not be \
                                     set (use "var" instead)`
                            );
        holder.set(val);
    }
}

class SimpleValue : Value {
    Object __val;

    oper init(Object val) : __val = val {}

    Object evaluate(EvalContext context) {
        return __val;
    }

    Object invoke(EvalContext context, Array[Object] args) {
        # Assume this is a functor.
        val := evaluate(context);
        func := Functor2[Object, EvalContext, Array[Object]].cast(val);
        return func(context, args);
    }

    void set(Object value) {
        __val = value;
    }
}

@abstract class Builtin : Value {
    Object evaluate(EvalContext context) {
        return this;
    }

    void set(Object val) {
        throw Exception('You can not set a builtin.');
    }
}

class PrintCmd : Builtin {
    Object invoke(EvalContext context, Array[Object] args) {
        for (arg :in args)
            cout `$arg`;
        return null;
    }
}

class FormatCmd : Builtin {
    Object invoke(EvalContext context, Array[Object] args) {
        StringFormatter tmp = {};
        for (arg :in args) {
            tmp `$arg`;
        }

        return tmp.string();
    }
}

class VarCmd : Builtin {
    Object invoke(EvalContext context, Array[Object] args) {
        if (args.count() != 2)
            throw Exception('usage: var <var> <value>');

        String var = String.cast(args[0], null);
        if (var is null)
            throw Exception('first argument of var must be a string');

        Object val = args[1];
        context.define(var, SimpleValue(val));
        return val;
    }
}

class SetCmd : Builtin {
    Object invoke(EvalContext context, Array[Object] args) {
        if (args.count() != 2)
            throw Exception('usage: set <var> <value>');

        String var = String.cast(args[0], null);
        if (var is null)
            throw Exception('first argument of set must be a string');

        Object val = args[1];
        context.set(var, val);
        return val;
    }
}

@abstract class Term {
    @abstract Object evaluate(EvalContext context);
}

## A single command.
class Command : Value {
    String command;
    Array[Term] args = {};

    oper init(String command) : command = command {}

    Object evaluate(EvalContext context) {
        cmdVal := context.lookUp(command);
        if (!cmdVal)
            throw Exception(FStr() `Invalid command: $command\n`);

        Array[Object] evaledArgs = {};
        for (arg :in args)
            evaledArgs.append(arg.evaluate(context));
        return cmdVal.invoke(context, evaledArgs);
    }

    # This is really just to implement Value's interface, but it has the
    # ineresting side-effect of letting us evaluate a command against a fully
    # evaluated set of arguments.
    Object invoke(EvalContext context, Array[Object] args) {
        cmdVal := context.lookUp(command);
        if (!cmdVal)
            throw Exception(FStr() `Invalid command: $command\n`);

        return cmdVal.invoke(context, args);
    }

    void set(Object value) {
        throw Exception('You may not set the value of a command.');
    }

    void formatTo(Formatter out) {
        out `$command`;
        for (arg :in args)
            out ` $arg`;
    }
}

## A Block of commands.
class Block {
    Array[Value] commands = {};

    Object evaluate(EvalContext context) {
        context.yield = false;
        context.block = commands;
        Object result;
        while (context.ip < commands.count()) {
            result = commands[context.ip++].evaluate(context);
            if (context.yield)
                return null;
        }
        return result;
    }

    @final void add(Value value) {
        commands.append(value);
    }

    void formatTo(Formatter out) {
        for (cmd :in commands)
            out `$cmd\n`;
    }
}

## An interpolated command: [do something]
class InterpolatedCommand : Term {
    Block contents;

    Object evaluate(EvalContext context) {
        return contents.evaluate(context);
    }
}

## A string without variable interpolations.
class LiteralString : Term {
    String val;

    oper init(String val) : val = val {}

    Object evaluate(EvalContext context) {
        return val;
    }

    void formatTo(Formatter out) {
        out `$(val.getRepr())`;
    }
}

#class InterpolatedString : Term {
#    Array[StringTerm
#    String value;
#
#    Object evaluate(EvalContext context) {
#        AppendBuffer result = {};
#        pos := value.lfind('$');
#        if (pos)
#            result.extend(value.slice(0, pos));

const int
    TOK_EOF = 0,
    TOK_STR = 1,
    TOK_SEMI = 2;

class Token {
    int type;
    String text;

    oper init(int type, String text) : type = type, text = text {}
}

class Toker {
    String contents;
    uint pos;

    oper init(String contents) : contents = contents {}

    Token getToken() {
        AppendBuffer result = {128};
        const int
            BASE = 0,

            # A word is a sequence of non-whitespace chars which may contain
            # interpolation sequences.
            WORD = 1;
        int state = BASE;
        while (true) {
            byte ch;
            if (pos == contents.size)
                ch = 0;
            else
                ch = contents[pos++];
            if (state == BASE) {
                if (!ch)
                    return Token(TOK_EOF, null);
                if (ch == b'\n' || ch == b';')
                    return Token(TOK_SEMI, String(1, ch));
                if (!isSpace(ch)) {
                    result.append(ch);
                    state = WORD;
                }
            } else if (state == WORD) {
                if (isSpace(ch) || !ch || ch == b';') {
                    # Back up so we can catch it if it's a newline.
                    if (ch) --pos;
                    return Token(TOK_STR, String(result, true));
                } else {
                    result.append(ch);
                }
            }
        }

        return null;
    }
}

class Parser {
    Toker toker;

    oper init(String contents) : toker(contents) {}

    Block parse() {
        Block result = {};
        Command cmd;

        while (true) {
            tok := toker.getToken();
            if (tok.type == TOK_EOF) {
                break;
            } else if (tok.type == TOK_STR) {
                if (!cmd)
                    cmd = Command(tok.text);
                else
                    cmd.args.append(LiteralString(tok.text));
            } else if (tok.type == TOK_SEMI) {
                # Add the current command.
                if (cmd) {
                    result.add(cmd);
                    cmd = null;
                }
            }
        }

        # Add the last command.
        if (cmd)
            result.add(cmd);

        return result;
    }
}

## Returns a new EvalContext containing all of the normal builtin functions.
EvalContext createRootContext() {
    ctx := EvalContext();
    ctx.define('print', PrintCmd());
    ctx.define('format', FormatCmd());
    ctx.define('var', VarCmd());
    ctx.define('set', SetCmd());
    return ctx;
}

Block compile(String expr) {
    Parser parser = {expr};
    return parser.parse();
}

Object eval(Block block, EvalContext context) {
    if (context is null)
        return block.evaluate(createRootContext());
    else
        return block.evaluate(context);
}

Object eval(Block block) { return eval(block, null); }

Object eval(String expr, EvalContext context) {
    block := compile(expr);
    return eval(block, context);
}

Object eval(String expr) { return eval(expr, null); }

#void repl() {
#    while (true) {
#        raw := readline('$ ');
#        if (raw is null)
#            return;
#        eval(String(raw, true));
#    }
#}
